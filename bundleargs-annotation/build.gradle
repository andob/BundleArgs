apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'

android {
    if (project.hasProperty('setup.compileSdk'))
    {
        compileSdkVersion setup.compileSdk
        buildToolsVersion setup.buildTools
        defaultConfig {
            minSdkVersion setup.minSdk
            targetSdkVersion setup.targetSdk
        }
    }
    else
    {
        compileSdkVersion 30
        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 30
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

publishing {
    publications {
        repositories.maven {
            url 'https://maven.andob.info/repository/open_source/'
            credentials {
                username "$System.env.MAVEN_PUBLISH_USERNAME"
                password "$System.env.MAVEN_PUBLISH_PASSWORD"
            }
        }

        android.libraryVariants.all { variant ->
            "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                from components.findByName("android${variant.name.capitalize()}")
                groupId 'ro.andob.bundleargs'
                artifactId 'bundleargs-annotation'
                version '2.1.0'
            }
        }
    }
}
